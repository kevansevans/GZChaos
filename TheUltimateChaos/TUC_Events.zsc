Class TUC_EventList play
{
	static TUC_EventList create()
	{
		TUC_EventList list = new('TUC_EventList');
		list.timemodifier = 1.0;
		list.cachedEffects.clear();
		return list;
	}
	
	double timemodifier;
	
	int timeLimit()
	{
		return int(Max(35 * 30 * timemodifier, 35 * 1));
	}
	
	Array<Int> cachedEffects;
	
	TUC_Event getEffect()
	{
		int event = -1;
		
		while (true)
		{
			event = random(0, E_NumEvents - 1);
			if (cachedEffects.size() == 0) break;
			switch (event)
			{
				case E_HalfTimerDuration:
					if (cachedEffects.find(E_QuarterTimerDuration) != cachedEffects.size()) continue;
					break;
				case E_QuarterTimerDuration:
					if (cachedEffects.find(E_HalfTimerDuration) != cachedEffects.size()) continue;
					break;
			}
			if (cachedEffects.find(event) == cachedEffects.size()) break;
		}
		
		cachedEffects.push(event);
		
		if (cachedEffects.size() > 20) {
			cachedEffects.delete(0);
			cachedEffects.shrinkToFit();
		}
	
		switch (event)
		{
			////////////////////////////////////////////////////////////////////////////////////////////////////
			//negative events
			////////////////////////////////////////////////////////////////////////////////////////////////////
			case E_TakeAllKeysAway:
				return TUC_TakeAllKeysAway.create();
			case E_ReplaceHealthWithBonuses:
				return TUC_ReplaceHealthWithBonuses.create();
			case E_ReplaceMedikitWithStimpack:
				return TUC_ReplaceMedikitWithStimpack.create();
			case E_SpawnGreatGodImp:
				return TUC_SpawnGreatGodImp.create();
			case E_TheFloorIsLava:
				return TUC_TheFloorIsLava.create();
			case E_TheFloorIsNukage:
				return TUC_TheFloorIsNukage.create();
			case E_TheFloorIsSludge:
				return TUC_TheFloorIsSludge.create();
			case E_DisableForwardBackwards:
				return TUC_DisableForwardBackwards.create();
			case E_DisableStrafingMovement:
				return TUC_DisableStrafingMovement.create();
			case E_ShrinkPickups:
				return TUC_ShrinkPickups.create();
			case E_GameEndPlayer:
				if (random(1, 100) == 100) return TUC_GameEndPlayer.create();
				else return TUC_FakeGameEndPlayer.create();
			////////////////////////////////////////////////////////////////////////////////////////////////////
			//neutral events
			////////////////////////////////////////////////////////////////////////////////////////////////////
			case E_TeleportPlayer:
				return TUC_TeleportPlayer.create();
			case E_Nothing:
				return TUC_DoNothing.create();
			case E_DoubleTimerDuration:
				return TUC_DoubleTimerDuration.create();
			case E_HalfTimerDuration:
				return TUC_HalfTimerDuration.create();
			case E_QuarterTimerDuration:
				return TUC_QuarterTimerDuration.create();
			case E_SetAllWallsToStartan:
				return TUC_SetAllWallsToStartan.create();
			case E_SetAllWallsToFireblu:
				return TUC_SetAllWallsToFireblu.create();
			case E_ShuffleKeyTextures:
				return TUC_ShuffleKeyTextures.create();
			////////////////////////////////////////////////////////////////////////////////////////////////////
			//positive events
			////////////////////////////////////////////////////////////////////////////////////////////////////
			case E_GiveMegaSphere:
				return TUC_GiveMegaSphere.create();
			case E_GiveLightAmp:
				return TUC_GiveLightAmp.create();
			case E_GiveRadSuit:
				return TUC_GiveRadSuit.create();
			case E_GiveInvulnerabilitySphere:
				return TUC_GiveInvulnerabilitySphere.create();
			case E_GivePistol:
				return TUC_GivePistol.create();
			case E_GiveShotgun:
				return TUC_GiveShotgun.create();
			case E_GiveSuperShotgun:
				return TUC_GiveSuperShotgun.create();
			case E_GiveChaingun:
				return TUC_GiveChaingun.create();
			case E_GiveRocketLauncher:
				return TUC_GiveRocketLauncher.create();
			case E_GivePlasmaRifle:
				return TUC_GivePlasmaRifle.create();
			case E_GiveBFG9000:
				return TUC_GiveBFG9000.create();
			case E_SpawnFriendlyGreatGodImp:
				return TUC_SpawnFriendlyGreatGodImp.create();
			////////////////////////////////////////////////////////////////////////////////////////////////////
			//Silly
			////////////////////////////////////////////////////////////////////////////////////////////////////
			case E_WideMonsters:
				return TUC_WideMonsters.create();
			case E_TallMonsters:
				return TUC_TallMonsters.create();
			case E_SpinningMonsters:
				return TUC_SpinningMonsters.create();
			case E_SetMusicToRUNNNN:
				return TUC_SetMusicToRUNNNN.create();
			case E_MisalignAllTextures:
				return TUC_MisalignAllTextures.create();
			case E_FakeGameEndPlayer:
				if (random(1, 100) == 100) return TUC_GameEndPlayer.create();
				else return TUC_FakeGameEndPlayer.create();
		}
		
		console.printf('Missing event case! Investigate: ' .. event);
		return TUC_DoNothing.create();
	}
}

enum E_EventType
{
	E_Instant,
	E_Timed,
	E_PerLevel,
	E_HiddenTimer,
	E_Permnanent
}

enum E_Event
{
	////////////////////////////////////////////////////////////////////////////////////////////////////
	//negative
	////////////////////////////////////////////////////////////////////////////////////////////////////
	E_TakeAllKeysAway,
	E_ReplaceHealthWithBonuses,
	E_ReplaceMedikitWithStimpack,
	E_SpawnGreatGodImp,
	E_TheFloorIsLava,
	E_TheFloorIsNukage,
	E_TheFloorIsSludge,
	E_DisableForwardBackwards,
	E_DisableStrafingMovement,
	E_ShrinkPickups,
	E_GameEndPlayer,
	////////////////////////////////////////////////////////////////////////////////////////////////////
	//neutral
	////////////////////////////////////////////////////////////////////////////////////////////////////
	E_TeleportPlayer,
	E_Nothing,
	E_DoubleTimerDuration,
	E_HalfTimerDuration,
	E_QuarterTimerDuration,
	E_SetAllWallsToStartan,
	E_SetAllWallsToFireblu,
	E_ShuffleKeyTextures,
	////////////////////////////////////////////////////////////////////////////////////////////////////
	//positive
	////////////////////////////////////////////////////////////////////////////////////////////////////
	E_GiveMegaSphere,
	E_GiveLightAmp,
	E_GiveRadSuit,
	E_GiveInvulnerabilitySphere,
	E_GivePistol,
	E_GiveShotgun,
	E_GiveSuperShotgun,
	E_GiveChaingun,
	E_GiveRocketLauncher,
	E_GivePlasmaRifle,
	E_GiveBFG9000,
	E_SpawnFriendlyGreatGodImp,
	////////////////////////////////////////////////////////////////////////////////////////////////////
	//silly
	////////////////////////////////////////////////////////////////////////////////////////////////////
	E_WideMonsters,
	E_TallMonsters,
	E_SpinningMonsters,
	E_SetMusicToRUNNNN,
	E_MisalignAllTextures,
	E_FakeGameEndPlayer,
	////////////////////////////////////////////////////////////////////////////////////////////////////
	//Number of Events
	////////////////////////////////////////////////////////////////////////////////////////////////////
	E_NumEvents
}

Class TUC_Event abstract play
{
	String eventname;
	Int duration;
	Int life;
	Bool activated;
	E_EventType type;
	abstract void eventFunc();
	abstract void closeFunc();
	
	void givePlayerItem(string _item)
	{
		let player = players[consoleplayer].mo;
		let item = Actor.spawn(_item);
		item.touch(player);
	}
	
	void giveAndSwitchTo(string _weapon)
	{
		let player = players[consoleplayer].mo;
		if (!player.countInv(_weapon))
		{
			let item = Actor.spawn(_weapon);
			item.touch(player);
		}
		player.A_SelectWeapon(_weapon);
	}
	
	bool checkIfReplaceable(String _textureName)
	{
		string texname = _texturename;
		switch (name(texname))
		{
			case 'BIGDOOR1':
			case 'BIGDOOR2':
			case 'BIGDOOR3':
			case 'BIGDOOR4':
			case 'BIGDOOR6':
			case 'BIGDOOR7':
			case 'DOOR1':
			case 'DOOR3':
			case 'DOORBLU':
			case 'DOORBLU2':
			case 'DOORRED':
			case 'DOORRED2':
			case 'DOORTRAK':
			case 'DOORYEL':
			case 'DOORYEL2':
			case 'EXITDOOR':
			case 'SPCDOOR1':
			case 'SPCDOOR2':
			case 'SPCDOOR3':
			case 'SPCDOOR4':
			case 'TEKBRON1':
			case 'TEKBRON2':
				return false;
		}
		
		texname.truncate(2);
		if (texname == "SW") return false;
		
		return true;
	}
}