Class TUC_AutoPong : TUC_Event
{
	override void eventFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
		player.giveInventory('TUC_PongToken', 1);
	}
	
	override void closeFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
		player.takeInventory('TUC_PongToken', 1);
	}
	
	static TUC_Event create()
	{
		TUC_Event event = new('TUC_AutoPong');
		event.eventname = "Pong";
		event.duration = 35 * 60 * 5;
		event.type = E_Timed;
		return event;
	}
}

Class TUC_PongToken : Inventory {}

Class TUC_ResetEffectTimers : TUC_Event
{
	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
			
		for (inventory item = player.inv; item != null; item = item.inv)
		{	
			if (item is "EffectInventory")
			{
				EffectInventory effect = EffectInventory(item);
				
				for (int i = 0; i < effect.events.size(); ++i)
				{
					if (effect.events[i].type == E_Timed)
					{
						effect.events[i].life = 0;
					}
				}
				
				break;
			}
		}
	}
	
	override void closeFunc()
	{
		
	}

	static TUC_Event create()
	{
		TUC_Event event = new('TUC_ResetEffectTimers');
		event.eventname = "Reset effect timers";
		event.duration = 35 * 60;
		event.type = E_Instant;
		return event;
	}
}

Class TUC_HideTimerAndEffects : TUC_Event
{
	override void eventFunc()
	{
		EventHandler.SendNetworkEvent("meta:hideeverything");
	}
	
	override void closeFunc()
	{
		EventHandler.SendNetworkEvent("meta:showeverything");
	}

	static TUC_Event create()
	{
		TUC_Event event = new('TUC_HideTimerAndEffects');
		event.eventname = "Hidden timer and effects";
		event.duration = 35 * 60 * 3;
		event.type = E_Timed;
		return event;
	}
}

Class TUC_DoubleTimerDuration : TUC_Event
{
	override bool verify()
	{
		bool result = Super.verify();
	
		PlayerPawn player = players[consoleplayer].mo;
		EffectInventory effects = EffectInventory(player.findInventory("EffectInventory"));
		
		for (int i = 0; i < effects.events.size(); ++i)
		{
			TUC_Event event = effects.events[i];
			if (event is "TUC_QuarterTimerDuration") return false;
			if (event is "TUC_HalfTimerDuration") return false;
		}
		
		return result;
	}

	override void eventFunc()
	{
		EventHandler.SendNetworkEvent("meta:slowdown:2");
		activated = true;
	}
	
	override void closeFunc()
	{
		EventHandler.SendNetworkEvent("meta:speedup:2");
	}
	
	static TUC_Event create()
	{
		TUC_Event event = new('TUC_DoubleTimerDuration');
		event.eventname = "1/2 timer speed";
		event.duration = 35 * 60 * 5;
		event.type = E_Timed;
		return event;
	}
}

Class TUC_HalfTimerDuration : TUC_Event
{
	override bool verify()
	{
		bool result = Super.verify();
	
		PlayerPawn player = players[consoleplayer].mo;
		EffectInventory effects = EffectInventory(player.findInventory("EffectInventory"));
		
		for (int i = 0; i < effects.events.size(); ++i)
		{
			TUC_Event event = effects.events[i];
			if (event is "TUC_DoubleTimerDuration") return false;
			if (event is "TUC_QuarterTimerDuration") return false;
		}
		
		return result;
	}

	override void eventFunc()
	{
		EventHandler.SendNetworkEvent("meta:speedup:2");
		activated = true;
	}
	
	override void closeFunc()
	{
		EventHandler.SendNetworkEvent("meta:slowdown:2");
	}
	
	static TUC_Event create()
	{
		TUC_Event event = new('TUC_HalfTimerDuration');
		event.eventname = "2x timer speed";
		event.duration = 35 * 60 * 3;
		event.type = E_Timed;
		return event;
	}
}

Class TUC_QuarterTimerDuration : TUC_Event
{
	override bool verify()
	{
		bool result = Super.verify();
	
		PlayerPawn player = players[consoleplayer].mo;
		EffectInventory effects = EffectInventory(player.findInventory("EffectInventory"));
		
		for (int i = 0; i < effects.events.size(); ++i)
		{
			TUC_Event event = effects.events[i];
			if (event is "TUC_DoubleTimerDuration") return false;
			if (event is "TUC_HalfTimerDuration") return false;
		}
		
		return result;
	}

	override void eventFunc()
	{
		EventHandler.SendNetworkEvent("meta:speedup:4");
		activated = true;
	}
	
	override void closeFunc()
	{
		EventHandler.SendNetworkEvent("meta:slowdown:4");
	}
	
	static TUC_Event create()
	{
		TUC_Event event = new('TUC_QuarterTimerDuration');
		event.eventname = "4x timer speed";
		event.duration = 35 * 60 * 3;
		event.type = E_Timed;
		return event;
	}
}

Class TUC_RollExtraEffects : TUC_Event
{
	int bonuseffects;

	override void eventFunc()
	{
		if (gametic % 5 == 0 && bonuseffects > 0)
		{
			EventHandler.SendNetworkEvent("rolleffect");
			--bonuseffects;
		}
	}
	
	override void closeFunc()
	{
		
	}
	
	static TUC_Event create()
	{
		TUC_Event event = new('TUC_RollExtraEffects');
		TUC_RollExtraEffects(event).bonuseffects = random(2, 5);
		event.eventname = "Roll " .. TUC_RollExtraEffects(event).bonuseffects .. " extra effects";
		event.duration = 35 * 60;
		event.type = E_HiddenTimer;
		return event;
	}
}

Class TUC_DoNothing : TUC_Event
{
	override void eventFunc(){}
	
	override void closeFunc() {}
	
	static TUC_Event create()
	{
		TUC_Event event = new('TUC_DoNothing');
		event.eventname = "Nothing.";
		event.duration = 35 * 60;
		event.type = E_Instant;
		return event;
	}
}