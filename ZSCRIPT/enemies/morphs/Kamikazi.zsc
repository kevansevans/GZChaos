Class GZC_KamikaziGuy : MorphedMonster
{
	Default
	{
		obituary "%o fell victim to a beheaded kamikazi!";
		health 20;
		radius 16;
		height 56;
		mass 100;
		speed 12;
		+Shootable;
		+Solid;
		+CanPushWalls;
		+CanUseWalls;
		+ActivateMCross;
		+CanPass;
		+FloorClip;
		+QuickToRetaliate;
	}
	states
	{
		Spawn:
			BKAM A 0 A_StopSound(CHAN_AUTO);
			BKAM AB 10 A_LookEx (0, 0, 0, 1024); 
			loop;
		See:
			BKAM A 0 A_StartSound("kamikazi/idle", CHAN_AUTO);
			BKAM A 2 A_FaceTarget();
			BKAM A 0 A_JumpIfCloser(56, "Melee");
			BKAM A 2 A_Recoil (-5);
			BKAM B 2 A_FaceTarget();
			BKAM A 0 A_JumpIfCloser(56, "Melee");
			BKAM B 2 A_Recoil (-5);
			BKAM C 2 A_FaceTarget();
			BKAM A 0 A_JumpIfCloser(56, "Melee");
			BKAM C 2 A_Recoil (-5);
			BKAM D 2 A_FaceTarget();
			BKAM A 0 A_JumpIfCloser(56, "Melee");
			BKAM D 2 A_Recoil (-5);
		CheckSee:
			BKAM D 0 A_CheckSight ("Navigate");
			goto See;
		Navigate:
			BKAM AABBCCDDAABBCCDDAABBCCDD 2 A_Chase;
			goto CheckSee;
		Melee:
			BKAM A 0 A_Die;
			stop;
		Death:
			BMBE M 0 A_SpawnItem ("KamikaziDamage", 0, 40);
			BMBE M 0 A_Stop();
			BMBE M 0 A_StopSound(CHAN_AUTO);
			BMBE M 5 bright A_StartSound("weapons/rocklx", CHAN_AUTO);
			BMBE N 5 bright;
			BMBE O 5 A_NoBlocking();
			BMBE PQRST 5;
			BMBE U -1;
			stop;
	}
}

Class KamikaziDamage : Actor
{
	Default
	{
		Radius 2;
		Height 2;
		Speed 0;
		Projectile;
	}
	States
	{
		Spawn:
			TNT1 A 1;
			TNT1 A 1 A_Explode (50, 96);
			stop;
  }
}