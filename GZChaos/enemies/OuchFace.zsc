Class OuchMFace : Actor
{
	int screamtimer, tickcount;
	double basescale;
	transient CVar ouch_scream;
	transient CVar ouch_nausea;

	override void Tick()
	{
		if ( screamtimer-- <= 0 )
		{
			A_StartSound("ouch/dspdiehi", CHAN_5, 0, FRandom[Ouch](0.05,0.3));
			screamtimer = Random[Ouch](1,10);
		}
		tickcount++;
		if ( !(tickcount%8) ) A_ChangeVelocity(vel.x+FRandom[Ouch](-10.0,10.0),vel.y+FRandom[Ouch](-10.0,10.0),vel.z+FRandom[Ouch](-20.0,20.0));
		A_SetScale(basescale*FRandom[Ouch](0.75,1.5),basescale*FRandom[Ouch](0.75,1.5));
		Super.Tick();
	}

	override void PostBeginPlay()
	{
		tickcount = Random[Ouch](0,100);
		basescale = FRandom[Ouch](0.1,2.0);
	}

	Default
	{
		Radius 8;
		Height 16;
		+NOBLOCKMAP
		+DROPOFF
	}
	States
	{
	Spawn:
		OUCF A -1;
		Stop;
	}
}