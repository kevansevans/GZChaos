Class GZC_RearViewMirror : EventHandler
{
	bool enabled;

	GZC_Camera camera;
	PlayerPawn player;

	override void WorldLoaded(WorldEvent _event)
	{
		camera = GZC_Camera(Actor.spawn('GZC_Camera'));
		player = players[consoleplayer].mo;
		camera.target = player;
	}
	
	override void WorldTick()
	{
		if (!enabled) return;
		if (!camera) return;
		
		TexMan.SetCameraToTexture(camera, 'TCAMTEX1', 90);
		
		FLineTraceData shot;
		player.LineTrace(player.angle, 48, -80, TRF_THRUACTORS, 0, 0, 0, shot);
		camera.setOrigin(shot.hitLocation, true);
		camera.A_FaceTarget();
	}

	override void renderOverlay(RenderEvent event)
	{
		if (!enabled) return;
		if (!camera) return;
	
		let mirror = TexMan.CheckForTexture('TCAMTEX1');
	
		double xscale = double(Screen.getWidth()) / 2560.0;
		double yscale = double(Screen.getHeight()) / 1440.0;
	
		Screen.DrawTexture(mirror, false, (Screen.getWidth() / 2) - (320 / 2 * 3 * xscale), 0, DTA_ScaleX, 3 * xscale, DTA_ScaleY, 3 * yscale);
	}
}

Class GZC_Camera : Actor
{
	Default
	{
		+NoGravity;
	}
}