Class GZC_ClearAllEffects : GZC_Event
{
	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
		let effects = EffectInventory(player.findInventory("EffectInventory"));
		
		foreach (event : effects.events)
		{
			event.life = event.duration;
		}
	}

	override void init()
	{
		eventname = "Clear all effects";
		duration = 35 * 60;
		type = E_Instant;		
	}
}

Class GZC_PortraitMode : GZC_Event
{
	override void openFunc()
	{
		GZC_PortraitModeEvent(EventHandler.find('GZC_PortraitModeEvent')).enabled = true;
	}
	
	override void closeFunc()
	{
		GZC_PortraitModeEvent(EventHandler.find('GZC_PortraitModeEvent')).enabled = false;
	}
	
	override void init()
	{
		eventname = "Portrait mode";
		duration = 35 * 60;
		deleteme = true;
		type = E_Timed;
	}
}

Class GZC_AutoPong : GZC_Event
{
	override void openFunc()
	{
		GZC_PongDrawer(EventHandler.find('GZC_PongDrawer')).enabled = true;
		GZC_PongDrawer(EventHandler.find('GZC_PongDrawer')).ponginit = false;
	}
	
	override void closeFunc()
	{
		GZC_PongDrawer(EventHandler.find('GZC_PongDrawer')).enabled = false;
	}
	
	override void init()
	{
		eventname = "Pong";
		duration = 35 * 60 * 2;
		deleteme = true;
		type = E_Timed;
	}
}

Class GZC_ResetEffectTimers : GZC_Event
{
	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
			
		for (inventory item = player.inv; item != null; item = item.inv)
		{	
			if (item is "EffectInventory")
			{
				EffectInventory effect = EffectInventory(item);
				
				for (int i = 0; i < effect.events.size(); ++i)
				{
					if (effect.events[i].type == E_Timed)
					{
						effect.events[i].life = 0;
					}
				}
				
				break;
			}
		}
	}
	
	override void closeFunc()
	{
		
	}

	override void init()
	{
		eventname = "Reset effect timers";
		duration = 35 * 60;
		type = E_Instant;
	}
}

Class GZC_HideTimerAndEffects : GZC_Event
{
	override void eventFunc()
	{
		GZC_EffectDisplay(EventHandler.find('GZC_EffectDisplay')).hiddeneffect = true;
		GZC_TimerDisplay(EventHandler.find('GZC_TimerDisplay')).hiddeneffect = true;
	}
	
	override void closeFunc()
	{
		GZC_EffectDisplay(EventHandler.find('GZC_EffectDisplay')).hiddeneffect = false;
		GZC_TimerDisplay(EventHandler.find('GZC_TimerDisplay')).hiddeneffect = false;
	}

	override void init()
	{
		eventname = "Hidden timer and effects";
		duration = 35 * 60 * random(1, 3);
		type = E_Timed;
	}
}
//Yeah, I didn't list this one on purpose, lol.
Class GZC_FuckYourGodmode : GZC_Event
{
	override void eventFunc()
	{
		let player = players[consoleplayer];
		player.cheats &= ~(CF_GODMODE);
	}

	override bool verify()
	{
		let player = players[consoleplayer];
		
		if (player.cheats & CF_GODMODE) return true;
		
		return false;
	}

	override void init()
	{
		eventname = "No god mode for you!";
		duration = 35 * 60;
		type = E_Instant;
	}
}

Class GZC_RollExtraEffects : GZC_Event
{
	int bonuseffects;

	override void eventFunc()
	{
		let event = GZC_Event.fetch(); 
	
		if (gametic % 5 == 0 && bonuseffects > 0)
		{
			event.addEffectToActive();
			--bonuseffects;
		}
	}
	
	override void closeFunc()
	{
		
	}
	
	override void init()
	{
		bonuseffects = random(2, 5);
		eventname = "Roll " .. bonuseffects .. " extra effects";
		duration = 35 * 60;
		type = E_HiddenTimer;
	}
}

Class GZC_DoNothing : GZC_Event
{
	override void eventFunc(){}
	
	override void closeFunc() {}
	
	override void init()
	{
		eventname = "Nothing.";
		duration = 35 * 60;
		type = E_Instant;
	}
}