Class GZC_PortraitMode : GZC_Event
{
	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
		player.giveInventory('GZC_PortraitModeToken', 1);
		activated = true;
	}
	
	override void closeFunc()
	{
		let player = players[consoleplayer].mo;
		player.takeInventory('GZC_PortraitModeToken', 999);
	}
	
	override void init()
	{
		eventname = "Portrait mode";
		duration = 35 * 60;
		type = E_Timed;
	}
}

class GZC_PortraitModeToken : inventory {}

Class GZC_AutoPong : GZC_Event
{
	override void eventFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
		player.giveInventory('GZC_PongToken', 1);
	}
	
	override void closeFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
		player.takeInventory('GZC_PongToken', 1);
	}
	
	override void init()
	{
		eventname = "Pong";
		duration = 35 * 60 * 2;
		type = E_Timed;
	}
}

Class GZC_PongToken : Inventory {}

Class GZC_ResetEffectTimers : GZC_Event
{
	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
			
		for (inventory item = player.inv; item != null; item = item.inv)
		{	
			if (item is "EffectInventory")
			{
				EffectInventory effect = EffectInventory(item);
				
				for (int i = 0; i < effect.events.size(); ++i)
				{
					if (effect.events[i].type == E_Timed)
					{
						effect.events[i].life = 0;
					}
				}
				
				break;
			}
		}
	}
	
	override void closeFunc()
	{
		
	}

	override void init()
	{
		eventname = "Reset effect timers";
		duration = 35 * 60;
		type = E_Instant;
	}
}

Class GZC_HideTimerToken : Inventory
{}

Class GZC_HideTimerAndEffects : GZC_Event
{
	override void eventFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
		player.giveInventory('GZC_HideTimerToken', 1);
	}
	
	override void closeFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
		player.takeInventory('GZC_HideTimerToken', 1);
	}

	override void init()
	{
		eventname = "Hidden timer and effects";
		duration = 35 * 60 * 3;
		type = E_Timed;
	}
}

Class GZC_FuckYourGodmode : GZC_Event
{
	override void eventFunc()
	{
		let player = players[consoleplayer];
		player.cheats &= ~(CF_GODMODE);
	}

	override bool verify()
	{
		let player = players[consoleplayer];
		
		if (player.cheats & CF_GODMODE) return true;
		
		return false;
	}

	override void init()
	{
		eventname = "No god mode for you!";
		duration = 35 * 60;
		type = E_Instant;
	}
}

Class GZC_RollExtraEffects : GZC_Event
{
	int bonuseffects;

	override void eventFunc()
	{
		let event = GZC_Event.fetch(); 
	
		if (gametic % 5 == 0 && bonuseffects > 0)
		{
			event.addEffectToActive();
			--bonuseffects;
		}
	}
	
	override void closeFunc()
	{
		
	}
	
	override void init()
	{
		bonuseffects = random(2, 5);
		eventname = "Roll " .. bonuseffects .. " extra effects";
		duration = 35 * 60;
		type = E_HiddenTimer;
	}
}

Class GZC_DoNothing : GZC_Event
{
	override void eventFunc(){}
	
	override void closeFunc() {}
	
	override void init()
	{
		eventname = "Nothing.";
		duration = 35 * 60;
		type = E_Instant;
	}
}