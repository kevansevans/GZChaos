Class GZC_MorphIntoDog : GZC_Event
{
	Class<Actor> original;

	override void eventFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
		
		player.A_Morph('DogPlayer', duration);
		
		activated = true;
	}
	
	override void closeFunc()
	{
	
	}

	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_MorphIntoDog');
		event.eventname = "Dog mode!";
		event.duration = 35 * 60 * 2;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_MorphIntoChicken : GZC_Event
{
	Class<Actor> original;

	override void eventFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
		
		player.A_Morph('ChickenPlayer', duration);
		
		activated = true;
	}
	
	override void closeFunc()
	{
	
	}

	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_MorphIntoChicken');
		event.eventname = "cockadoodledoo!";
		event.duration = 35 * 60 * 2;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_DoubleRunning : GZC_Event
{
	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
		let def = getDefaultByType(player.getClass());
		
		player.forwardMove1 = def.forwardMove1 * 4;
		player.forwardMove2 = def.forwardMove2 * 2;
		player.sideMove1 = def.sideMove1 * 4;
		player.sideMove2 = def.sideMove2 * 2;
	}
	
	override void closeFunc()
	{
		let player = players[consoleplayer].mo;
		let def = getDefaultByType(player.getClass());
		
		player.forwardMove1 = def.forwardMove1;
		player.forwardMove2 = def.forwardMove2;
		player.sideMove1 = def.sideMove1;
		player.sideMove2 = def.sideMove2;
	}

	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_DoubleRunning');
		event.eventname = "Give it the gas!";
		event.duration = 35 * 60 * 2;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_DisableRunning : GZC_Event
{
	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
		let def = getDefaultByType(player.getClass());
		
		player.forwardMove2 = def.forwardMove2 / 2;
		player.sideMove2 = def.sideMove2 / 2;
	}
	
	override void closeFunc()
	{
		let player = players[consoleplayer].mo;
		let def = getDefaultByType(player.getClass());
		
		player.forwardMove2 = def.forwardMove2;
		player.sideMove2 = def.sideMove2;
	}

	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_DisableRunning');
		event.eventname = "No need to rush";
		event.duration = 35 * 60 * 2;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_StartRecording : GZC_Event
{
	//stupid gzdoom not letting me use vector3's in an array
	Array<Double> posx;
	Array<Double> posy;
	Array<Double> posz;
	Array<Double> angles;
	
	int st;

	override void eventFunc()
	{
		if (!posx.size())
		{
			posx.clear();
			posy.clear();
			posz.clear();
			angles.clear();
			st = 0;
		}
		
		PlayerPawn player = players[consoleplayer].mo;
		
		switch (st)
		{
			case 0:
			
				Vector3 checkpoint = player.pos;
				Double checkangle = player.angle;
				
				posx.push(checkpoint.x);
				posy.push(checkpoint.y);
				posz.push(checkpoint.z);
				
				angles.push(checkangle);
				
				if (life == duration - 1)
				{
					st = 1;
					life = 0;
					eventname = "<< Rewinding";
				}
				break;
			case 1:
			
				int index = posx.size() - 1;
				
				player.setOrigin((posx[index], posy[index], posz[index]), true);
				player.angle = angles[index];
				
				posx.delete(index);
				posy.delete(index);
				posz.delete(index);
				
				angles.delete(index);
				
				posx.shrinkToFit();
				posy.shrinkToFit();
				posz.shrinkToFit();
				
				angles.shrinkToFit();
				
				break;
		}
	}
		
	override void closeFunc()
	{
	
	}
	
	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_StartRecording');
		event.eventname = "Recording";
		event.duration = 35 * 15;
		event.type = E_Timed;
		event.deleteme = true;
		return event;
	}
}

Class GZC_ScardeyItems : GZC_Event
{
	override void eventFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
		
		ThinkerIterator it = ThinkerIterator.Create("Actor");
		Actor mo;
		
		while ((mo = Actor(it.Next ())))
		{
			if (mo is "Inventory")
			{
				if (player.distance3D(mo) < 256)
				{
					mo.bSlidesOnWalls = true;
					mo.bNoBlockMonst = true;
					mo.thrust(15, player.angleTo(mo));
				}
			}
		}
	}
	
	override void closeFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
	}
	
	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_ScardeyItems');
		event.eventname = "Pickups are scared of player";
		event.duration = 35 * 60 * 2;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_RightJoyconDrift : GZC_Event
{
	double drift;
	double angle;
	
	override void eventFunc()
	{
		PlayerInfo player = players[consoleplayer];
		double xspeed = cos(angle) * drift;
		double yspeed = sin(angle) * drift;
		
		if (player.cmd.pitch == 0 && player.cmd.yaw == 0)
		{
			player.mo.angle += (xspeed / 1000);
			if (!player.centering) player.mo.pitch += (yspeed / 1000);
		
			player.mo.CheckPitch();
		}
	}
	
	override void closeFunc()
	{
	
	}
	
	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_RightJoyconDrift');
		GZC_RightJoyconDrift(event).drift = random(400, 600);
		GZC_RightJoyconDrift(event).angle = random(0, 359);
		event.eventname = "Right Joycon drift";
		event.duration = 35 * 60 * 2;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_LeftJoyconDrift : GZC_Event
{
	double drift;
	double angle;
	
	override void eventFunc()
	{
		PlayerInfo player = players[consoleplayer];
		double xspeed = cos(angle) * drift;
		
		if (player.cmd.forwardmove == 0 && player.cmd.sidemove == 0)
		{
			player.mo.thrust(xspeed / 2000, (xspeed / 2000) + player.mo.angle);
		}
	}
	
	override void closeFunc()
	{
	
	}
	
	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_LeftJoyconDrift');
		GZC_LeftJoyconDrift(event).drift = random(400, 600);
		GZC_LeftJoyconDrift(event).angle = random(0, 359);
		event.eventname = "Left Joycon drift";
		event.duration = 35 * 60 * 2;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_DisableForwardBackwards : GZC_Event
{
	override bool verify()
	{
		bool result = Super.verify();
	
		PlayerPawn player = players[consoleplayer].mo;
		EffectInventory effects = EffectInventory(player.findInventory("EffectInventory"));
		
		for (int i = 0; i < effects.events.size(); ++i)
		{
			GZC_Event event = effects.events[i];
			if (event is "GZC_DisableStrafingMovement") return false;
		}
		
		return result;
	}

	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
		player.forwardMove1 = player.forwardMove2 = 0;
	}
	
	override void closeFunc()
	{
		let player = players[consoleplayer].mo;
		let def = getDefaultByType(player.getClass());
		
		player.forwardMove1 = def.ForwardMove1;
		player.forwardMove2 = def.ForwardMove2;
	}
	
	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_DisableForwardBackwards');
		event.eventname = "Disable Forward/Backwards movement";
		event.duration = 35 * 60;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_DisableStrafingMovement : GZC_Event
{
	override bool verify()
	{
		bool result = Super.verify();
	
		PlayerPawn player = players[consoleplayer].mo;
		EffectInventory effects = EffectInventory(player.findInventory("EffectInventory"));
		
		for (int i = 0; i < effects.events.size(); ++i)
		{
			GZC_Event event = effects.events[i];
			if (event is "GZC_DisableForwardBackwards") return false;
		}
		
		return result;
	}

	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
		player.sideMove1 = player.sideMove2 = 0;
	}
	
	override void closeFunc()
	{
		let player = players[consoleplayer].mo;
		let def = getDefaultByType(player.getClass());
		
		player.sideMove1 = def.sideMove1;
		player.sideMove2 = def.sideMove2;
	}
	
	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_DisableStrafingMovement');
		event.eventname = "Disable strafing movement";
		event.duration = 35 * 60;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_InvertPlayerMovement : GZC_Event
{
	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
		let def = getDefaultByType(player.getClass());

		player.forwardMove1 = def.forwardMove1 * -1;
		player.forwardMove2 = def.forwardMove2 * -1;
		player.sideMove1 = def.sideMove1 * -1;
		player.sideMove2 = def.sideMove2 * -1;
	}
	
	override void closeFunc()
	{
		let player = players[consoleplayer].mo;
		let def = getDefaultByType(player.getClass());

		player.forwardMove1 = def.forwardMove1;
		player.forwardMove2 = def.forwardMove2;
		player.sideMove1 = def.sideMove1;
		player.sideMove2 = def.sideMove2;
	}
	
	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_InvertPlayerMovement');
		event.eventname = "Inverted movement";
		event.duration = 35 * 60;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_GameEndPlayer : GZC_Event
{
	int startframe;
	bool screm;

	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
		if (!screm)
		{
			player.A_PlayerScream();
			screm = true;
		}
		if (gametic - startframe >= 35 * 1)
		{
			player.A_Explode(999, 64);
			activated = true;
		}
		else
		{
			Player.A_SetMugshotState("Ouch");
		}
	}
	
	override void closeFunc()
	{
	
	}

	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_GameEndPlayer');
		GZC_GameEndPlayer(event).startframe = gametic;
		event.eventname = "Player commits suicide";
		event.duration = 35 * 90;
		event.type = E_HiddenTimer;
		return event;
	}
}

Class GZC_FakeGameEndPlayer : GZC_Event
{
	int startframe;
	bool screm;

	override void eventFunc()
	{
		let player = players[consoleplayer].mo;
		if (!screm)
		{
			player.A_PlayerScream();
			screm = true;
		}
		if (gametic - startframe <= 35 * 1)
		{
			Player.A_SetMugshotState("Ouch");
		}
		else if (gametic - startframe >= 35 * 3)
		{
			eventname = "Fake suicide :)";
			activated = true;
		}
	}
	
	override void closeFunc()
	{
	
	}

	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_FakeGameEndPlayer');
		GZC_FakeGameEndPlayer(event).startframe = gametic;
		event.eventname = "Player commits suicide";
		event.duration = 35 * 90;
		event.type = E_HiddenTimer;
		return event;
	}
}

Class GZC_CantStopFiring : GZC_Event
{
	override void eventFunc()
	{
		PlayerInfo player = players[consoleplayer];
		player.cmd.buttons |= (BT_ATTACK | BT_ALTATTACK);
		player.mo.checkWeaponFire();
	}
	
	override void closeFunc()
	{
	
	}

	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_CantStopFiring');
		event.eventname = "Can't stop shooting!";
		event.duration = 35 * 60;
		event.type = E_Timed;
		return event;
	}
}

Class GZC_SuperIcePhysics : GZC_Event
{
	override void eventFUnc()
	{
		PlayerInfo player = players[consoleplayer];
		player.mo.bNoFriction = true;
	}
	
	override void closeFunc()
	{
		PlayerInfo player = players[consoleplayer];
		player.mo.bNoFriction = false;
	}
	
	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_SuperIcePhysics');
		event.eventname = "Disabled player friction";
		event.duration = (35 * 60 * 2) + (35 * 30);
		event.type = E_Timed;
		return event;
	}
}

Class GZC_FallDamage : GZC_Event
{
	override void eventFUnc()
	{
		PlayerInfo player = players[consoleplayer];
		player.mo.bFallDamage = true;
	}
	
	override void closeFunc()
	{
		PlayerInfo player = players[consoleplayer];
		player.mo.bFallDamage = false;
	}
	
	Static GZC_Event create()
	{
		GZC_Event event = new('GZC_FallDamage');
		event.eventname = "Player takes fall damage";
		event.duration = (35 * 60 * 2) + (35 * 30);
		event.type = E_Timed;
		return event;
	}
}

Class GZC_TeleportPlayer : GZC_Event
{
	override void eventFunc()
	{
		PlayerPawn player = players[consoleplayer].mo;
		
		while (true)
		{
			int rng = random(0, level.Sectors.size() - 1);
			let c = level.Sectors[rng].centerspot;
			if (level.IsPointInLevel((c.x, c.y, level.Sectors[rng].floorplane.ZAtPoint(c))))
			{
				double ceilheight = level.Sectors[rng].ceilingplane.ZAtPoint(c);
				double floorheight = level.Sectors[rng].floorplane.ZAtPoint(c);
				if (ceilheight - floorheight > 64)
				{
					Player.SetOrigin((c.x, c.y, level.Sectors[rng].floorplane.ZAtPoint(c)), false);
					Actor.spawn('TeleportFog', (c.x, c.y, level.Sectors[rng].floorplane.ZAtPoint(c)));
					break;
				}
			}
		}
	}
	
	override void closeFunc() {}
	
	static GZC_Event create()
	{
		GZC_Event event = new('GZC_TeleportPlayer');
		event.eventname = "Teleport to random sector";
		event.duration = 35 * 90;
		event.type = E_Instant;
		return event;
	}
}